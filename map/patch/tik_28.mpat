#new:Header $Header
{
	[MainScript] $Script_Main
	[Background] 00000000
	[EntryList] $EntryList
	[EntryCount] 3
    [MapTattle]  $Function_GetTattle
}

#new:EntryList $EntryList
{
	~Vec4f:Entry0
	~Vec4f:Entry1
	~Vec4f:Entry2
}

#new:Script_Main $Script_Main
{
	Set   *GB_WorldLocation  .Location:ToadTownTunnels
	Call  SetCamPerspective  ( .Cam:Default 00000003 00000019 00000010 00001000 )
	Call  SetCamBGColor      ( .Cam:Default 00000000 00000000 00000000 )
	Call  SetCamEnabled      ( .Cam:Default .True )
	Call  SetCamLeadPlayer   ( .Cam:Default .False )
	Exec  $Script_MakeEntities
	Exec  $Script_SetupMusic
	Exec  $Script_SetupTexturePan
	Exec  $Script_EnterMap
	Return
	End
}

#new:Script $Script_SetupMusic
{
	Call  SetMusicTrack ( 00000000 .Song:ToadTownTunnels 00000000 00000008 )
	Call  ClearAmbientSounds ( 250` )
	Return
	End
}

#new:Script $Script_EnterMap
{
    Call    GetEntryID ( *Var0 )
    If *Var0 == ~Entry:Entry0
        Set *Var0 $Script_CreateExitTriggers
        Exec EnterWalk
        Call EnableModel ( ~Model:pipe2 .False )
    Else % Pipes
        Set     *VarA $Script_CreateExitTriggers
        Exec    $Script_EnterVerticalPipe
    EndIf
    Bind  $Script_UpdateRespawnPoint_1 .Trigger:FloorTouch ~Collider:safe1 00000001 00000000
    Bind  $Script_UpdateRespawnPoint_2 .Trigger:FloorTouch ~Collider:safe2 00000001 00000000
    Bind  $Script_MarioDrowned .Trigger:FloorTouch ~Collider:respawn 00000001 00000000
    %
    Call  MakeItemEntity ( .Item:DefendPlusP ~Vec3d:ItemPos .ItemSpawnMode:Fixed_NeverVanish *GF_TIK28_Item )
    %
    Call  ParentColliderToModel ( ~Collider:respawn ~Model:water )
    Call  TranslateGroup ( ~Model:water_group 0 -180` 0 )
    Call  UpdateColliderTransform ( ~Collider:respawn )
    %
    Call    EnableGroup ( ~Model:waterPipe1 .False )
    Call    EnableGroup ( ~Model:waterPipe2 .False )
    Call    EnableGroup ( ~Model:waterPipe3 .False )
    Call    EnableGroup ( ~Model:waterPipe4 .False )
    Call    ModifyColliderFlags ( .False ~Collider:pipesWater 7FFFFE00 )
    Exec    $Script_UpdateWaterLevel
	Return
	End
}

#new:Script $Script_UpdateWaterLevel
{
    Loop
        If *MapVar[1] == .False % raise water
            Wait    50`
            Exec    $Script_Water_Start
            Call    MakeLerp    	( -180` 0 300` .Easing:Linear )
            Loop
                Call    UpdateLerp
                Call    TranslateGroup ( ~Model:water_group 0 *Var0 0 )
                Call    UpdateColliderTransform ( ~Collider:respawn )
                Wait    1
                If  *Var1  == .False
                    BreakLoop
                EndIf
            EndLoop
            Exec $Script_Water_Stop
            Set *MapVar[1] .True
        Else % drain water
            Wait    100`
            Call    PlaySound ( 0042 )
            Call    MakeLerp    	( 0 -180` 200` .Easing:Linear )
            Loop
                Call    UpdateLerp
                Call    TranslateGroup ( ~Model:water_group 0 *Var0 0 )
                Call    UpdateColliderTransform ( ~Collider:respawn )
                Wait    1
                If  *Var1  == .False
                    BreakLoop
                EndIf
            EndLoop
            Set *MapVar[1] .False
        EndIf
    EndLoop
    Return
    End
}

#new:Script $Script_UpdateRespawnPoint_1
{
    Set *MapVar[0] 0
    Return
    End
}

#new:Script $Script_UpdateRespawnPoint_2
{
    Set *MapVar[0] 1
    Return
    End
}

#new:Script $Script_Water_Start
{
    Call    PlaySound ( 0021 )
    Call    ModifyColliderFlags ( .True ~Collider:pipesWater 7FFFFE00 )
    Call    EnableGroup ( ~Model:waterPipe1 .True )
    Call    EnableGroup ( ~Model:waterPipe2 .True )
    Call    EnableGroup ( ~Model:waterPipe3 .True )
    Call    EnableGroup ( ~Model:waterPipe4 .True )
    Call    ScaleGroup ( ~Model:waterPipe1 .False *Fixed[1.0] *Fixed[1.0] )
    Call    ScaleGroup ( ~Model:waterPipe2 .False *Fixed[1.0] *Fixed[1.0] )
    Call    ScaleGroup ( ~Model:waterPipe3 .False *Fixed[1.0] *Fixed[1.0] )
    Call    ScaleGroup ( ~Model:waterPipe4 .False *Fixed[1.0] *Fixed[1.0] )
    SetF    *Var0 *Fixed[0.0]
    Thread
        Loop 20`
            AddF    *Var0 *Fixed[0.05]
            Call    ScaleGroup ( ~Model:waterPipe1 *Var0 *Fixed[1.0] *Fixed[1.0] )
            Call    ScaleGroup ( ~Model:waterPipe2 *Var0 *Fixed[1.0] *Fixed[1.0] )
            Call    ScaleGroup ( ~Model:waterPipe3 *Var0 *Fixed[1.0] *Fixed[1.0] )
            Call    ScaleGroup ( ~Model:waterPipe4 *Var0 *Fixed[1.0] *Fixed[1.0] )
            Wait    1
        EndLoop
        Call    ScaleGroup ( ~Model:waterPipe1 *Fixed[1.0] *Fixed[1.0] *Fixed[1.0] )
        Call    ScaleGroup ( ~Model:waterPipe2 *Fixed[1.0] *Fixed[1.0] *Fixed[1.0] )
        Call    ScaleGroup ( ~Model:waterPipe3 *Fixed[1.0] *Fixed[1.0] *Fixed[1.0] )
        Call    ScaleGroup ( ~Model:waterPipe4 *Fixed[1.0] *Fixed[1.0] *Fixed[1.0] )
    EndThread
    Wait    8
    Set     *Var0 ~Model:takib_1
    Set     *Var1 15` % duration
    Set     *Var2 0   % start opacity
    Set     *Var3 3F  % target opacity
    ExecWait $Script_AdvancedUpdateModelAlpha
    Return
    End
}

#new:Script $Script_Water_Stop
{
    Call    StopSound ( 0021 )
    Call    ModifyColliderFlags ( .False ~Collider:pipesWater 7FFFFE00 )
    SetF    *Var0 *Fixed[1.0]
    Thread
        Loop 20`
            SubF    *Var0 *Fixed[0.05]
            Call    ScaleGroup ( ~Model:waterPipe1 *Var0 *Fixed[1.0] *Fixed[1.0] )
            Call    ScaleGroup ( ~Model:waterPipe2 *Var0 *Fixed[1.0] *Fixed[1.0] )
            Call    ScaleGroup ( ~Model:waterPipe3 *Var0 *Fixed[1.0] *Fixed[1.0] )
            Call    ScaleGroup ( ~Model:waterPipe4 *Var0 *Fixed[1.0] *Fixed[1.0] )
            Wait    1
        EndLoop
        Call    ScaleGroup ( ~Model:waterPipe1 .False *Fixed[1.0] *Fixed[1.0] )
        Call    ScaleGroup ( ~Model:waterPipe2 .False *Fixed[1.0] *Fixed[1.0] )
        Call    ScaleGroup ( ~Model:waterPipe3 .False *Fixed[1.0] *Fixed[1.0] )
        Call    ScaleGroup ( ~Model:waterPipe4 .False *Fixed[1.0] *Fixed[1.0] )
        Call    EnableGroup ( ~Model:waterPipe1 .False )
        Call    EnableGroup ( ~Model:waterPipe2 .False )
        Call    EnableGroup ( ~Model:waterPipe3 .False )
        Call    EnableGroup ( ~Model:waterPipe4 .False )
    EndThread
    Wait    8
    Set     *Var0 ~Model:takib_1
    Set     *Var1 15` % duration
    Set     *Var2 3F  % start opacity
    Set     *Var3 0   % target opacity
    ExecWait $Script_AdvancedUpdateModelAlpha
    Return
    End
}

#new:Script $Script_MarioDrowned
{
    Call    PlaySound ( 20E3 )
    If *VarA == .Partner:Lakilester
        Call    $WriteAddress ( 802BFF14 2 00000003 .False .False ) % unmount laki
    Else
        % partner wasn't using lakilester
        Call    DisablePlayerInput ( .True )
        Loop
            Call    GetPlayerActionState ( *Var0 )
            If *Var0 == .ActionState:Idle
                BreakLoop
            EndIf
            Wait    1
        EndLoop
        Call    DisablePlayerPhysics ( .True )
        Thread
            Loop 5
                Call    GetPlayerPos ( *Var0 *Var1 *Var2 )
                Sub     *Var1 1
                Call    SetPlayerPos ( *Var0 *Var1 *Var2 )
                Wait    1
            EndLoop
        EndThread
        Call    SetPlayerAnimation ( 00010002 )
        Call    SetPlayerAnimation ( 0001001B )
    EndIf
    SetF	*Var0 *Fixed[25.0] % speed
    SetF	*Var1 *Fixed[255.0]  % dark
    ExecWait $Script_FadeScreenOut
    If *VarA != .Partner:Lakilester
        Call    DisablePlayerPhysics ( .False )
    EndIf
    Switch *MapVar[0]
        Case == 0
            Call    SetPlayerPos ( ~Vec3d:Entry0 )
        Case == 1
            Call    SetPlayerPos ( ~Vec3d:JumpPos_Spring4 )
    EndSwitch
    Switch *VarA
        CaseOR == .Partner:Bow
        CaseOR == .Partner:Watt
            Call    $WriteBitFlag ( 8010EFCC .False 3 2 .True ) % stop using partner
        EndCaseGroup
    EndSwitch
    Call    TeleportPartnerToPlayer
    Call    InterpPlayerYaw ( 90` 0 )
    Wait    5
    If *VarA != .Partner:Lakilester
        Call    SetPlayerAnimation ( 00010002 )
        Call    SetPlayerAnimation ( 0001000F )
    EndIf
    SetF	*Var0 *Fixed[25.0] % speed
    SetF	*Var1 *Fixed[5.0]  % fully bright
    Exec    $Script_FadeScreenIn
    Wait    10`
    If *VarA == .Partner:Lakilester
        Return
    Else
        Call    SetPlayerAnimation ( 00010030 )
        Wait    15`
        Call    SetPlayerAnimation ( 00010031 )
        Thread
            Call    PlaySound ( 0172 )
            Wait    12`
            Call    PlaySound ( 0172 )
        EndThread
        Wait    25`
        Call    SetPlayerAnimation ( 00010002 )
        Call    DisablePlayerInput ( .False )
    EndIf    
    Return
    End
}

#new:Script $Script_CreateExitTriggers
{
    Bind  $Script_Exit0 .Trigger:FloorAbove ~Collider:Exit0 00000001 00000000
    Bind  $Script_Exit1 .Trigger:FloorTouch ~Collider:Exit1 00000001 00000000
	Return
	End
}

#new:Script $Script_Exit0
{
    SetGroup  0000001B
    Call  UseExitHeading    ( 0000003C ~Entry:Entry0 )
    Exec  ExitWalk
    Call  GotoMap   ( "tik_23" 00000002 )
    Wait  100`
    Return
    End
}

#new:Script $Script_Exit1
{
    SetGroup 0000001B
	Set   *VarA ~Entry:Entry1
	Set   *VarB ~Collider:Exit1
	Set   *VarC $Script_GoToMapPipe
    Set   *VarD ~Entry:Entry2
	ExecWait  $Script_ExitVerticalPipe
	Return
	End
}

#new:Script $Script_GoToMapPipe
{
    Call  GotoMap   ( "tik_28" *VarD )
    Wait  100`
    Return
    End
}

% Entities

#new:Script $Script_MakeEntities
{
	Call  MakeEntity    ( .Entity:ScriptSpring ~Vec4d:Entity_Spring1 80000000 )
	Call  AssignScript    ( $Script_ScriptedSpring_Entity_Spring1 )
	Call  MakeEntity    ( .Entity:ScriptSpring ~Vec4d:Entity_Spring2 80000000 )
	Call  AssignScript    ( $Script_ScriptedSpring_Entity_Spring2 )
	Call  MakeEntity    ( .Entity:ScriptSpring ~Vec4d:Entity_Spring3 80000000 )
	Call  AssignScript    ( $Script_ScriptedSpring_Entity_Spring3 )
	Call  MakeEntity    ( .Entity:ScriptSpring ~Vec4d:Entity_Spring4 80000000 )
	Call  AssignScript    ( $Script_ScriptedSpring_Entity_Spring4 )
    %
    Call  MakeEntity  	    ( .Entity:SuperBlock ~Vec4d:Entity_Shine 80000000 )
    Set   *MapVar[2] *Var0
    Call  AssignBlockFlag   ( *GF_TIK28_Shine )
    Call  AssignScript 	    ( $Script_Entity_Shine1 )
	Return
	End
}

#new:Script $Script_Entity_Shine1
{
    Set *Var0 *MapVar[2] % entityID
    SetConst *Var1 *GF_TIK28_Shine % flag
    ExecWait $Script_ShineBlock
    Return
    End
}

#new:Script $Script_ScriptedSpring_FollowCam
{
	Loop
		Call  GetPlayerPos  ( *Var[0] *Var[1] *Var[2] )
		Call  SetCamTarget  ( .Cam:Default *Var[0] *Var[1] *Var[2] )
		Wait  1`
	EndLoop
	Return
	End
}
#new:Script $Script_ScriptedSpring_Entity_Spring1
{
	Call  DisablePlayerInput    ( .True )
	Call  DisablePlayerPhysics  ( .True )
	Call  SetPlayerActionState  ( .ActionState:Launch )
	Wait  1`
	Exec  $Script_ScriptedSpring_FollowCam *Var[A]
	Call  SetPlayerJumpscale    ( *Fixed[0.7] )
	Call  PlayerJump            ( ~Vec3d:JumpPos_Spring1 30` )
	Kill  *Var[A]
	Call  SetPlayerActionState  ( .ActionState:Idle )
	Call  DisablePlayerPhysics  ( .False )
	Call  DisablePlayerInput    ( .False )
	Return
	End
}

#new:Script $Script_ScriptedSpring_Entity_Spring2
{
	Call  DisablePlayerInput    ( .True )
	Call  DisablePlayerPhysics  ( .True )
	Call  SetPlayerActionState  ( .ActionState:Launch )
	Wait  1`
	Exec  $Script_ScriptedSpring_FollowCam *Var[A]
	Call  SetPlayerJumpscale    ( *Fixed[0.7] )
	Call  PlayerJump            ( ~Vec3d:JumpPos_Spring2 30` )
	Kill  *Var[A]
	Call  SetPlayerActionState  ( .ActionState:Idle )
	Call  DisablePlayerPhysics  ( .False )
	Call  DisablePlayerInput    ( .False )
	Return
	End
}

#new:Script $Script_ScriptedSpring_Entity_Spring3
{
	Call  DisablePlayerInput    ( .True )
	Call  DisablePlayerPhysics  ( .True )
	Call  SetPlayerActionState  ( .ActionState:Launch )
	Wait  1`
	Exec  $Script_ScriptedSpring_FollowCam *Var[A]
	Call  SetPlayerJumpscale    ( *Fixed[0.7] )
	Call  PlayerJump            ( ~Vec3d:JumpPos_Spring3 30` )
	Kill  *Var[A]
	Call  SetPlayerActionState  ( .ActionState:Idle )
	Call  DisablePlayerPhysics  ( .False )
	Call  DisablePlayerInput    ( .False )
	Return
	End
}

#new:Script $Script_ScriptedSpring_Entity_Spring4
{
	Call  DisablePlayerInput    ( .True )
	Call  DisablePlayerPhysics  ( .True )
	Call  SetPlayerActionState  ( .ActionState:Launch )
	Wait  1`
	Exec  $Script_ScriptedSpring_FollowCam *Var[A]
	Call  SetPlayerJumpscale    ( *Fixed[0.7] )
	Call  PlayerJump            ( ~Vec3d:JumpPos_Spring4 30` )
	Kill  *Var[A]
	Call  SetPlayerActionState  ( .ActionState:Idle )
	Call  DisablePlayerPhysics  ( .False )
	Call  DisablePlayerInput    ( .False )
	Return
	End
}

% Panners

#new:Script $Script_TexturePan_Update
{
    SetGroup  00000000
    If  *Var[5]  ==  00000001
        If  *Var[6]  ==  00000001
            If  *Var[7]  ==  00000001
                If  *Var[8]  ==  00000001
                    Call  $Function_TexturePan_Simple
                    Return
                EndIf
            EndIf
        EndIf
    EndIf
    Call  $Function_TexturePan_Complex
    Return
    End
}

#new:Script $Script_SetupTexturePan
{
	Call     802C9000 	( ~Model:water 0 )
	Call     802C9000 	( ~Model:takib_1 2 )
	Call     802C9000 	( ~Model:taki_1 1 )
	Call     802C9000 	( ~Model:taki_2 1 )
	Call     802C9000 	( ~Model:taki_3 1 )
	Call     802C9000 	( ~Model:taki_4 1 )
	Thread
		Set  *Var[0] 0
		Set  *Var[1] 46
		Set  *Var[2] 64
		Set  *Var[3] 64
		Set  *Var[4] 82
		Set  *Var[5] 1
		Set  *Var[6] 1
		Set  *Var[7] 1
		Set  *Var[8] 1
		Set  *Var[9] 0
		Set  *Var[A] 0
		Set  *Var[B] 0
		Set  *Var[C] 0
		Exec $Script_TexturePan_Update
	EndThread
	Thread
		Set  *Var[0] 1
		Set  *Var[1] FFFFFFCE
		Set  *Var[2] FFFFFC7C
		Set  *Var[3] FFFFFFBA
		Set  *Var[4] FFFFFB50
		Set  *Var[5] 1
		Set  *Var[6] 1
		Set  *Var[7] 1
		Set  *Var[8] 1
		Set  *Var[9] 0
		Set  *Var[A] 0
		Set  *Var[B] 0
		Set  *Var[C] 0
		Exec $Script_TexturePan_Update
	EndThread
	Thread
		Set  *Var[0] 2
		Set  *Var[1] 0
		Set  *Var[2] FFFFFA88
		Set  *Var[3] FFFFF5D8
		Set  *Var[4] FFFFFFC4
		Set  *Var[5] 1
		Set  *Var[6] 1
		Set  *Var[7] 1
		Set  *Var[8] 1
		Set  *Var[9] 0
		Set  *Var[A] 0
		Set  *Var[B] 0
		Set  *Var[C] 0
		Exec $Script_TexturePan_Update
	EndThread
	Return
	End
}

#new:Function $Function_TexturePan_Simple
{
    ADDIU       SP, SP, FFE8
    SW          S0, 10 (SP)
    COPY        S0, A0
    SW          RA, 14 (SP)
    LW          V1, A8 (S0)
    LW          V0, 88 (S0)
    ADDU        V1, V1, V0
    BGEZ        V1, .o30
    SW          V1, A8 (S0)
    LUI         V0, 2
    BEQ         R0, R0, .o40
    ADDU        V0, V1, V0
    .o30
    LUI         A0, 2
    SLT         V0, A0, V1
    BEQ         V0, R0, .o44
    SUBU        V0, V1, A0
    .o40
    SW          V0, A8 (S0)
    .o44
    LW          V1, AC (S0)
    LW          V0, 8C (S0)
    ADDU        V1, V1, V0
    BGEZ        V1, .o64
    SW          V1, AC (S0)
    LUI         V0, 2
    BEQ         R0, R0, .o74
    ADDU        V0, V1, V0
    .o64
    LUI         A0, 2
    SLT         V0, A0, V1
    BEQ         V0, R0, .o78
    SUBU        V0, V1, A0
    .o74
    SW          V0, AC (S0)
    .o78
    LW          V1, B0 (S0)
    LW          V0, 90 (S0)
    ADDU        V1, V1, V0
    BGEZ        V1, .o98
    SW          V1, B0 (S0)
    LUI         V0, 2
    BEQ         R0, R0, .oA8
    ADDU        V0, V1, V0
    .o98
    LUI         A0, 2
    SLT         V0, A0, V1
    BEQ         V0, R0, .oAC
    SUBU        V0, V1, A0
    .oA8
    SW          V0, B0 (S0)
    .oAC
    LW          V1, B4 (S0)
    LW          V0, 94 (S0)
    ADDU        V1, V1, V0
    BGEZ        V1, .oCC
    SW          V1, B4 (S0)
    LUI         V0, 2
    BEQ         R0, R0, .oDC
    ADDU        V0, V1, V0
    .oCC
    LUI         A0, 2
    SLT         V0, A0, V1
    BEQ         V0, R0, .oE0
    SUBU        V0, V1, A0
    .oDC
    SW          V0, B4 (S0)
    .oE0
    LW          A0, 84 (S0)
    JAL         8011BC1C
    LW          A1, A8 (S0)
    LW          A0, 84 (S0)
    JAL         8011BC34
    LW          A1, AC (S0)
    LW          A0, 84 (S0)
    JAL         8011BC4C
    LW          A1, B0 (S0)
    LW          A0, 84 (S0)
    JAL         8011BC64
    LW          A1, B4 (S0)
    LW          RA, 14 (SP)
    LW          S0, 10 (SP)
    CLEAR       V0
    JR          RA
    ADDIU       SP, SP, 18
}

#new:Function $Function_TexturePan_Complex
{
    ADDIU       SP, SP, FFE8
    SW          S0, 10 (SP)
    COPY        S0, A0
    BEQ         A1, R0, .o24
    SW          RA, 14 (SP)
    SW          R0, 70 (S0)
    SW          R0, 74 (S0)
    SW          R0, 78 (S0)
    SW          R0, 7C (S0)
    .o24
    LW          V0, 70 (S0)
    BNE         V0, R0, .o64
    NOP
    LW          V1, A8 (S0)
    LW          V0, 88 (S0)
    ADDU        V1, V1, V0
    BGEZ        V1, .o50
    SW          V1, A8 (S0)
    LUI         V0, 2
    BEQ         R0, R0, .o60
    ADDU        V0, V1, V0
    .o50
    LUI         A0, 2
    SLT         V0, A0, V1
    BEQ         V0, R0, .o64
    SUBU        V0, V1, A0
    .o60
    SW          V0, A8 (S0)
    .o64
    LW          V0, 74 (S0)
    BNE         V0, R0, .oA4
    NOP
    LW          V1, AC (S0)
    LW          V0, 8C (S0)
    ADDU        V1, V1, V0
    BGEZ        V1, .o90
    SW          V1, AC (S0)
    LUI         V0, 2
    BEQ         R0, R0, .oA0
    ADDU        V0, V1, V0
    .o90
    LUI         A0, 2
    SLT         V0, A0, V1
    BEQ         V0, R0, .oA4
    SUBU        V0, V1, A0
    .oA0
    SW          V0, AC (S0)
    .oA4
    LW          V0, 78 (S0)
    BNE         V0, R0, .oE4
    NOP
    LW          V1, B0 (S0)
    LW          V0, 90 (S0)
    ADDU        V1, V1, V0
    BGEZ        V1, .oD0
    SW          V1, B0 (S0)
    LUI         V0, 2
    BEQ         R0, R0, .oE0
    ADDU        V0, V1, V0
    .oD0
    LUI         A0, 2
    SLT         V0, A0, V1
    BEQ         V0, R0, .oE4
    SUBU        V0, V1, A0
    .oE0
    SW          V0, B0 (S0)
    .oE4
    LW          V0, 7C (S0)
    BNE         V0, R0, .o124
    NOP
    LW          V1, B4 (S0)
    LW          V0, 94 (S0)
    ADDU        V1, V1, V0
    BGEZ        V1, .o110
    SW          V1, B4 (S0)
    LUI         V0, 2
    BEQ         R0, R0, .o120
    ADDU        V0, V1, V0
    .o110
    LUI         A0, 2
    SLT         V0, A0, V1
    BEQ         V0, R0, .o124
    SUBU        V0, V1, A0
    .o120
    SW          V0, B4 (S0)
    .o124
    LW          A0, 84 (S0)
    JAL         8011BC1C
    LW          A1, A8 (S0)
    LW          A0, 84 (S0)
    JAL         8011BC34
    LW          A1, AC (S0)
    LW          A0, 84 (S0)
    JAL         8011BC4C
    LW          A1, B0 (S0)
    LW          A0, 84 (S0)
    JAL         8011BC64
    LW          A1, B4 (S0)
    LW          V0, 70 (S0)
    LW          V1, 78 (S0)
    LW          A0, 98 (S0)
    ADDIU       V0, V0, 1
    SW          V0, 70 (S0)
    LW          V0, 74 (S0)
    ADDIU       V1, V1, 1
    SW          V1, 78 (S0)
    LW          V1, 70 (S0)
    ADDIU       V0, V0, 1
    SW          V0, 74 (S0)
    LW          V0, 7C (S0)
    SLT         V1, V1, A0
    ADDIU       V0, V0, 1
    BNE         V1, R0, .o198
    SW          V0, 7C (S0)
    SW          R0, 70 (S0)
    .o198
    LW          V0, 74 (S0)
    LW          V1, 9C (S0)
    SLT         V0, V0, V1
    BEQL        V0, R0, .o1AC
    SW          R0, 74 (S0)
    .o1AC
    LW          V0, 78 (S0)
    LW          V1, A0 (S0)
    SLT         V0, V0, V1
    BEQL        V0, R0, .o1C0
    SW          R0, 78 (S0)
    .o1C0
    LW          V0, 7C (S0)
    LW          V1, A4 (S0)
    SLT         V0, V0, V1
    BEQL        V0, R0, .o1D4
    SW          R0, 7C (S0)
    .o1D4
    LW          RA, 14 (SP)
    LW          S0, 10 (SP)
    CLEAR       V0
    JR          RA
    ADDIU       SP, SP, 18
}

% Pipes code

#new:Script $Script_EnterVerticalPipe
{
	Call  DisablePlayerInput    ( .True )
	Call  DisablePlayerPhysics  ( .True )
	Call  DisablePartnerAI      ( 00000000 )
	Call  HidePlayerShadow      ( .True )
	Call  SetPlayerAnimation    ( .PlayerAnim:StandStill )
	Call  GetCurrentPartnerID   ( *Var0 )
	If  *Var0  !=  .Partner:None % 0
		Call  EnableNpcShadow   ( .Npc:Partner .False )
		Call  SetNpcPos ( .Npc:Partner 00000000 -1000` 00000000 )
	EndIf
	Call  GetEntryID    ( *Var0 )
	Call  $Function_Pipe_GetEntryCoords
	Sub   *Var2  40`
	Call  SetPlayerPos  ( *Var1 *Var2 *Var3 )
	Call  InterpPlayerYaw   ( *Var4 00000000 )
	Call  PlaySound     ( 00000163 )
	Call  802D286C      ( 00000100 )
	Call  802D2520      ( 00010000 00000005 00000002 00000001 00000001 00000000 )
	Call  GetPlayerPos  ( *Var0 *Var1 *Var2 )
	Loop  40`
		Add   *Var1  00000001
		Call  SetPlayerPos  ( *Var0 *Var1 *Var2 )
		Wait  1`
	EndLoop
	Wait  3`
	Call  GetCurrentPartnerID   ( *Var0 )
	If  *Var0  !=  .Partner:None % 0
		Thread
			Call  DisablePartnerAI  ( 00000000 )
			Call  GetPlayerPos      ( *Var0 *Var1 *Var2 )
			Sub   *Var2  00000003
			Call  SetNpcPos ( .Npc:Partner *Var0 *Var1 *Var2 )
			Call  SetNpcFlagBits    ( .Npc:Partner 00000002 .False )
			Call  EnablePartnerAI
			Call  EnableNpcShadow   ( .Npc:Partner .True )
		EndThread
	EndIf
	Wait  2`
	Call  802D2520  ( 00010000 00000000 00000000 00000000 00000000 00000000 )
	Wait  1`
	Call  SetPlayerAnimation    ( 00010002 )
    %
    ExecWait $Script_HidePipe
    %
	Call  DisablePlayerPhysics  ( .False )
	Call  DisablePlayerInput    ( .False )
	Call  HidePlayerShadow      ( .False )
	/%Label  0
	Call  $Function_Pipe_GetCurrentFloor
	Wait  1`
	If  *Var0  !=  FFFFFFFF % FFFFFFFF
		Goto  0
	EndIf%/
	Exec  *VarA
	Return
	End
}

#new:Script $Script_HidePipe
{
    Exec  $Script_ScriptedSpring_FollowCam *Var8
    Call  SetPlayerJumpscale    ( *Fixed[0.3] )
	Call  PlayerJump            ( ~Vec3d:JumpPos_Pipe 30` )
    Kill  *Var8
    Call  PlaySound   	( 0163 )
    Call  MakeLerp		( 0` -50` 30` .Easing:CosInOut )
    Loop
        Call    UpdateLerp
        Call	TranslateModel	( ~Model:pipe2 0 *Var0 0 )
        Wait    1
        If  *Var1  == .False
            BreakLoop
        EndIf
    EndLoop
    Return
    End
}

#new:Script $Script_ExitVerticalPipe
{
	Call  $Function_Pipe_CheckDownInput
	If  *Var0  ==  00000000
		Return
	EndIf
	Call  GetCurrentPartner ( *Var0 )
	If  *Var0  !=  .Partner:None
		Call  GetCurrentPartnerID   ( *Var1 )
		If  *Var1  !=  .Partner:Watt % 6
			Return
		Else
			Call  802D2B6C
			Call  DisablePlayerInput    ( .True )
		EndIf
	Else
		Call  DisablePlayerInput    ( .True )
	EndIf
	SetGroup  0000001B
	Call  $Function_Pipe_SetAnimFlag
	Call  DisablePlayerPhysics  ( .True )
	Call  HidePlayerShadow  ( .True )
	Set   *Var0  *VarA
	Call  $Function_Pipe_GetEntryCoords
	Call  PlayerMoveTo      ( *Var1 *Var3 00000003 )
	Set   *Var0  *VarA
	Call  $Function_Pipe_GetEntryCoords
	Call  SetPlayerPos      ( *Var1 *Var2 *Var3 )
	Call  SetPlayerFlagBits ( 00200000 .True )
	Call  $Function_Pipe_GetCameraYaw
	Call  InterpPlayerYaw   ( *Var0 00000000 )
	Wait  2`
	Call  SetPlayerFlagBits ( 00200000 .False )
	Call  PlaySound ( 00000163 )
	Call  GetPlayerPos      ( *Var0 *Var1 *Var2 )
	Thread
		Wait  4`
		Loop  40`
			Sub   *Var1  00000001
			Call  SetPlayerPos  ( *Var0 *Var1 *Var2 )
			Wait  1`
		EndLoop
	EndThread
	Call  802D286C  ( 00000800 )
	Call  802D2520  ( 00010002 00000005 00000002 00000001 00000001 00000000 )
	Wait  25`
	ExecWait  *VarC
	Return
	End
}

% Pipe functions

#new:Function $Function_Pipe_SetAnimFlag
{
	LAW       V0, 800F7B30
	LW        V1, 4 (V0)
	LUI       A0, 10
	OR        V1, V1, A0
	SW        V1, 4 (V0)
	JR        RA
	ADDIU     V0, R0, 2
}

#new:Function $Function_Pipe_GetCurrentFloor
{
	LAH       V0, 8015A552
	SW        V0, 84 (A0)
	JR        RA
	ADDIU     V0, R0, 2
}

#new:Function $Function_Pipe_CheckDownInput
{
	LAH       V1, 8015A552
	ADDIU     SP, SP, FFE8
	SW        S0, 10 (SP)
	COPY      S0, A0
	SW        RA, 14 (SP)
	LW        V0, B0 (S0)
	BEQ       V1, V0, .o2C
	ADDIU     V0, R0, 2
	BEQ       R0, R0, .oAC
	SW        R0, 84 (S0)
	.o2C
	LAW       V1, 8007419C
	LB        V0, 40 (V1)
	LB        A3, 44 (V1)
	BLTZL     V0, .o44
	SUBU      V0, R0, V0
	.o44
	BNE       V0, R0, .o54
	NOP
	BEQL      A3, R0, .oAC
	CLEAR     V0
	.o54
	MTC1      V0, F4
	NOP
	CVT.S.W   F4, F4
	MFC1      A2, F4
	MTC1      A3, F4
	NOP
	CVT.S.W   F4, F4
	MTC1      R0, F12
	MFC1      A3, F4
	JAL       ~Func:atan2
	MOV.S     F14, F12
	LIF       F2, 60.0
	NOP
	C.LT.S    F0, F2
	NOP
	BC1F      .oA8
	ADDIU     V0, R0, 1
	SW        V0, 84 (S0)
	BEQ       R0, R0, .oAC
	ADDIU     V0, R0, 2
	.oA8
	CLEAR     V0
	.oAC
	LW        RA, 14 (SP)
	LW        S0, 10 (SP)
	JR        RA
	ADDIU     SP, SP, 18
}

#new:Function $Function_Pipe_GetEntryCoords
{
	ADDIU     SP, SP, FFE0
	SW        S1, 14 (SP)
	COPY      S1, A0
	SW        RA, 1C (SP)
	SW        S2, 18 (SP)
	JAL       ~Func:get_current_map_header
	SW        S0, 10 (SP)
	COPY      A0, S1
	LIO       A1, *Var0
	JAL       ~Func:get_variable
	COPY      S2, V0
	COPY      A0, S1
	LUI       A1, FE36
	LW        V1, 14 (S2)
	SLL       S0, V0, 4
	ADDU      V1, S0, V1
	LWC1      F0, 0 (V1)
	TRUNC.W.S F2, F0
	MFC1      A2, F2
	JAL       ~Func:set_variable
	ORI       A1, A1, 3C81 % Var1
	COPY      A0, S1
	LW        V0, 14 (S2)
	LUI       A1, FE36
	ADDU      V0, S0, V0
	LWC1      F0, 4 (V0)
	TRUNC.W.S F2, F0
	MFC1      A2, F2
	JAL       ~Func:set_variable
	ORI       A1, A1, 3C82 % Var2
	COPY      A0, S1
	LW        V0, 14 (S2)
	LUI       A1, FE36
	ADDU      V0, S0, V0
	LWC1      F0, 8 (V0)
	TRUNC.W.S F2, F0
	MFC1      A2, F2
	JAL       ~Func:set_variable
	ORI       A1, A1, 3C83 % Var3
	COPY      A0, S1
	LW        V0, 14 (S2)
	LUI       A1, FE36
	ADDU      S0, S0, V0
	LWC1      F0, C (S0)
	TRUNC.W.S F2, F0
	MFC1      A2, F2
	JAL       ~Func:set_variable
	ORI       A1, A1, 3C84 % Var4
	LW        RA, 1C (SP)
	LW        S2, 18 (SP)
	LW        S1, 14 (SP)
	LW        S0, 10 (SP)
	ADDIU     V0, R0, 2
	JR        RA
	ADDIU     SP, SP, 20
}

#new:Function $Function_Pipe_GetCameraYaw
{
	LAW       V1, 80077410
	LIF       F12, 180.0
	ADDIU     SP, SP, FFE8
	SW        RA, 14 (SP)
	SW        S0, 10 (SP)
	SLL       V0, V1, 2
	ADDU      V0, V0, V1
	SLL       V0, V0, 2
	SUBU      V0, V0, V1
	SLL       V1, V0, 3
	ADDU      V0, V0, V1
	SLL       V0, V0, 3
	LTF       F0, V0 (800B1DEC)
	ADD.S     F12, F0, F12
	JAL       ~Func:clamp_angle
	COPY      S0, A0
	TRUNC.W.S F2, F0
	SWC1      F2, 84 (S0)
	LW        RA, 14 (SP)
	LW        S0, 10 (SP)
	ADDIU     V0, R0, 2
	JR        RA
	ADDIU     SP, SP, 18
}

#new:Function $Function_Pipe_GetPointInFrontOfPlayer
{
	ADDIU     SP, SP, FFD8
	SW        S0, 18 (SP)
	COPY      S0, A0
	SW        RA, 1C (SP)
	SDC1      F20, 20 (SP)
	LW        V0, C (S0)
	JAL       ~Func:get_float_variable
	LW        A1, 0 (V0)
	ADDIU     A0, SP, 10
	LIA       V0, 8010EFC8
	MFC1      A2, F0
	LWC1      F0, 28 (V0) % player X
	LWC1      F2, 30 (V0) % player Z
	SWC1      F0, 10 (SP) % arg0 for add_vec2D_polar
	SWC1      F2, 14 (SP) % arg1 for add_vec2D_polar
	LW        A3, 80 (V0)
	LWC1      F20, 2C (V0)
	JAL       ~Func:add_vec2D_polar
	ADDIU     A1, SP, 14
	COPY      A0, S0
	LUI       A1, FE36
	LW        A2, 10 (SP)
	JAL       ~Func:set_float_variable
	ORI       A1, A1, 3C80 % Var0
	COPY      A0, S0
	LUI       A1, FE36
	MFC1      A2, F20
	JAL       ~Func:set_float_variable
	ORI       A1, A1, 3C81 % Var1
	COPY      A0, S0
	LUI       A1, FE36
	LW        A2, 14 (SP)
	JAL       ~Func:set_float_variable
	ORI       A1, A1, 3C82 % Var2
	LW        RA, 1C (SP)
	LW        S0, 18 (SP)
	LDC1      F20, 20 (SP)
	ADDIU     V0, R0, 2
	JR        RA
	ADDIU     SP, SP, 28
}

%

#new:Function $Function_GetTattle
{
LIO       V0, $String_Tattle
JR        RA
RESERVED
}

#string $String_Tattle {
[Style Tattle][EnableCDownNext]
It seems that all the water from[BR]
the sewers is drained here.[BR]
[Wait][Next]
We better use those springs if[BR]
we don't want to get drowned in[BR]
stinky water Mario!
[Wait][End]      
}