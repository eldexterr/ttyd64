#new:Header $Header
{
	[MainScript] $Script_Main
	[Background] 00000000
	[EntryList] $EntryList
	[EntryCount] 1
    [MapTattle]  $Function_GetTattle
}

#new:EntryList $EntryList
{
	~Vec4f:Entry0
}

#new:Script_Main $Script_Main
{
	Set   *GB_WorldLocation  .Location:ToadTownTunnels
	Call  SetCamPerspective  ( .Cam:Default 00000003 00000019 00000010 00001000 )
	Call  SetCamBGColor      ( .Cam:Default 00000000 00000000 00000000 )
	Call  SetCamEnabled      ( .Cam:Default .True )
	Call  SetCamLeadPlayer   ( .Cam:Default .False )
	Exec  $Script_MakeEntities
	Exec  $Script_SetupMusic
	Exec  $Script_SetupTexturePan
    Exec  $Script_EnterMap
    Call  MakeNpcs ( .False $NpcGroupList_tik27 )
    Set   *Var0 $Script_CreateExitTriggers
    Exec  EnterWalk
	Return
	End
}

#new:Script $Script_MakeEntities
{
	Call  MakeEntity    ( .Entity:ScriptSpring ~Vec4d:Entity_Spring 80000000 )
	Call  AssignScript    ( $Script_ScriptedSpring_Entity_Spring )
    If *GF_TIK27_Puzzle == .False
        Call  MakeEntity    ( .Entity:RedSwitch ~Vec4d:Entity_Switch1 80000000 )
        Call  AssignScript  ( $Script_Switch1 )
        Call  MakeEntity    ( .Entity:RedSwitch ~Vec4d:Entity_Switch2 80000000 )
        Call  AssignScript  ( $Script_Switch2 )
        Call  MakeEntity    ( .Entity:RedSwitch ~Vec4d:Entity_Switch3 80000000 )
        Call  AssignScript  ( $Script_Switch3 )
    Else
        Call  MakeItemEntity ( .Item:DeepFocusP ~Vec3d:ItemPos .ItemSpawnMode:Fixed_NeverVanish *GF_TIK27_Reward )
    EndIf
    %
    Call  MakeEntity  	    ( .Entity:SuperBlock ~Vec4d:Entity_Shine 80000000 )
    Set   *MapVar[2] *Var0
    Call  AssignBlockFlag   ( *GF_TIK27_Shine )
    Call  AssignScript 	    ( $Script_Entity_Shine1 )
	Return
	End
}

#new:Script $Script_Entity_Shine1
{
    Set *Var0 *MapVar[2] % entityID
    SetConst *Var1 *GF_TIK27_Shine % flag
    ExecWait $Script_ShineBlock
    Return
    End
}

#new:Script $Script_Switch1
{
    Set *Var0 1
    Set *Var1 155`
    Set *Var2 3 % previous switch
    ExecWait $Script_RedSwitch
    Return
    End
}

#new:Script $Script_Switch2
{
    Set *Var0 2
    Set *Var1 .False
    Set *Var2 1 % previous switch
    ExecWait $Script_RedSwitch
    Return
    End
}

#new:Script $Script_Switch3
{
    Set *Var0 3
    Set *Var1 160`
    Set *Var2 .False % previous switch
    ExecWait $Script_RedSwitch
    Return
    End
}

#new:Script $Script_RedSwitch
{
    Set *Var6 *Var0
    Set *Var7 *Var1
    Set *Var8 *Var2
    % check if player pressed the same switch once again
    If *MapVar[0] == *Var6
        Return
    EndIf
    % check if a timer was active
    If *MapVar[1] != .False
        Kill *MapVar[1]
        Set  *MapVar[1] .False
    EndIf
    % Check if this one was the correct switch
    If *MapVar[0] == *Var8 % correct switch
        Set *Var5 .True % correct switch
    Else % wrong switch
        Set *Var5 .False % incorrect
    EndIf
    % switch (bool)
    If *Var5 == .True % This switch was the correct one
        Call PlaySound ( 021C ) % correct
        Switch *Var6
            Case == 1
                Set *MapFlag[00] .True
            Case == 2
                Set *MapFlag[01] .True
            Case == 3
                Set *MapFlag[02] .True
        EndSwitch
        Set *MapVar[0] *Var6 % last switch pressed
        % check if this one is the last switch
        Set *Var0 *MapFlag[00]
        Add *Var0 *MapFlag[01]
        Add *Var0 *MapFlag[02]
        If *Var0 >= 3
            % last switch
            ExecWait $Script_PuzzleSolved
        Else
            Exec $Script_StartTimer *MapVar[1]
        EndIf
    Else % incorrect switch
        Call PlaySound ( 021D ) % error
        Set *MapFlag[00] .False
        Set *MapFlag[01] .False
        Set *MapFlag[02] .False
        Set *MapVar[0] .False
    EndIf
    Return
    End
}

#new:Script $Script_StartTimer
{
    Set *Var5 .False
    Loop *Var7
        Add *Var5 1
        Set *Var0 5 % lowest amount of frames to wait
        Set *Var1 *Var7
        Div *Var1 10`
        Add *Var0 *Var1
        If *Var5 >= *Var0
            Set     *Var5 .False
            Call	PlaySound ( 0005 ) % warning
        EndIf
        Wait    1
    EndLoop
    % Too late
    Set *MapFlag[00] .False
    Set *MapFlag[01] .False
    Set *MapFlag[02] .False
    Set *MapVar[0] .False
    Set *MapVar[1] .False
    Call PlaySound ( 021D ) % error
    Return
    End
}

#new:Script $Script_PuzzleSolved
{
    Call    DisablePlayerInput ( .True )
    Call    DisablePlayerPhysics ( .True )
    Call    DisablePartnerAI ( .True )
    % move camera
    Call    PlaySound ( 000B )
    Set     *GF_TIK27_Puzzle .True
    Call    GetPlayerPos ( *Var0 *Var1 *Var2 )
    Call    UseSettingsFrom ( .Cam:Default *Var0 *Var1 *Var2 )
	Call    SetCamSpeed 	( .Cam:Default *Fixed[7.0] )
	Call    SetPanTarget 	( .Cam:Default ~Vec3d:ItemPos )
    Call    PanToTarget 	( .Cam:Default .Easing:Linear 00000001 )
    Wait    10`
    %
    Set     *Var0 ~PosXd:ItemPos
    Set     *Var1 ~PosYd:ItemPos
    Set     *Var2 ~PosZd:ItemPos
    Add     *Var1 200` % y
    Call    MakeItemEntity ( .Item:DeepFocusP *Var0 *Var1 *Var2 .ItemSpawnMode:Fall_NeverVanish *GF_TIK27_Reward )
    Wait    50`
    % dissapear switches
    %
    % switch 1
    Set     *Var2 ~PosZd:Entity_Switch1
    Add     *Var2 8 % z
    Call    PlaySound  ( .Sound:SmokeBurst )
	Call    PlayEffect ( ~FX:BigSmokePuff ~PosXd:Entity_Switch1 ~PosYd:Entity_Switch1 *Var2 .False .False .False .False .False .False .False .False .False )
    % switch 2
    Set     *Var2 ~PosZd:Entity_Switch2
    Add     *Var2 8 % z
    Call    PlaySound  ( .Sound:SmokeBurst )
	Call    PlayEffect ( ~FX:BigSmokePuff ~PosXd:Entity_Switch2 ~PosYd:Entity_Switch2 *Var2 .False .False .False .False .False .False .False .False .False )
    % switch 3
    Set     *Var2 ~PosZd:Entity_Switch3
    Add     *Var2 8 % z
    Call    PlaySound  ( .Sound:SmokeBurst )
	Call    PlayEffect ( ~FX:BigSmokePuff ~PosXd:Entity_Switch3 ~PosYd:Entity_Switch3 *Var2 .False .False .False .False .False .False .False .False .False )
    % hide entities
    Wait    5
    % switch 1
    Call    $GetEntityPointer ( 00000001 *Var0 )
    Call    $WriteAddress ( *Var0 2 -1000.0 4C .False ) % posY
    % switch 2
    Call    $GetEntityPointer ( 00000002 *Var0 )
    Call    $WriteAddress ( *Var0 2 -1000.0 4C .False ) % posY
    % switch 3
    Call    $GetEntityPointer ( 00000003 *Var0 )
    Call    $WriteAddress ( *Var0 2 -1000.0 4C .False ) % posY
    % restart camera
    Call    SetCamSpeed ( .Cam:Default *Fixed[1.0] )
    Call    ResetCam ( .Cam:Default *Fixed[5.0] )
    Wait    15`
    Call    DisablePlayerInput ( .False )
    Call    DisablePlayerPhysics ( .False )
    Call    DisablePartnerAI ( .False )
    Return
    End
}

#new:Script $Script_SetupMusic
{
	Call  SetMusicTrack ( 00000000 .Song:ToadTownTunnels 00000000 00000008 )
	Call  ClearAmbientSounds ( 250` )
	Return
	End
}

#new:Script $Script_ScriptedSpring_FollowCam
{
	Loop
		Call  GetPlayerPos  ( *Var[0] *Var[1] *Var[2] )
		Call  SetCamTarget  ( .Cam:Default *Var[0] *Var[1] *Var[2] )
		Wait  1`
	EndLoop
	Return
	End
}
#new:Script $Script_ScriptedSpring_Entity_Spring
{
	Call  DisablePlayerInput    ( .True )
	Call  DisablePlayerPhysics  ( .True )
	Call  SetPlayerActionState  ( .ActionState:Launch )
	Wait  1`
	Exec  $Script_ScriptedSpring_FollowCam *Var[A]
	Call  SetPlayerJumpscale    ( *Fixed[0.7] )
	Call  PlayerJump            ( ~Vec3d:JumpPos_Spring 30` )
	Kill  *Var[A]
	Call  SetPlayerActionState  ( .ActionState:Idle )
	Call  DisablePlayerPhysics  ( .False )
	Call  DisablePlayerInput    ( .False )
	Return
	End
}

#new:Script $Script_EnterMap
{
    Call  ModifyColliderFlags   ( 00000003 ~Collider:sushiePad 00000004 )
    Call  ModifyColliderFlags   ( 00000000 ~Collider:sushiePad 00080000 )
	Return
	End
}

#new:Script $Script_Exit_Entry0
{
	SetGroup 0000001B
	Call    UseExitHeading ( 60` ~Entry:Entry0 )
	Exec    ExitWalk
	Call    GotoMap   ( "tik_10" ~Entry:tik_10:Entry2 )
	Wait    100`
	Return
	End
}

#new:Script $Script_CreateExitTriggers
{
	Bind  $Script_Exit_Entry0 .Trigger:FloorAbove ~Collider:Exit2 00000001 00000000
	Return
	End
}

% NPCs

#new:NpcGroupList $NpcGroupList_tik27
{
00000001 $NpcGroup_SpikedGoomba 1A100001
00000000 00000000 00000000
}

% Code taken from tik_05

#new:NpcGroup $NpcGroup_SpikedGoomba
{
	00000000 $NpcSettings_80241CE0 ~Vec3f:NPC_SpikedGoomba_00 % 220 -10 -80
	00002C00 00000000 00000000 00000000 0000010E
	~Items:20:DizzyDial:5:SleepySheep:5
	~HP:Standard:2 ~FP:Standard:2 ~CoinBonus:1:2
	~Movement:NPC_SpikedGoomba_00
	~AnimationTable:NPC_SpikedGoomba_00 % .Sprite:SpikedGoomba
	00000000 00000000 00000000 00000000 % no tattle string
}

#new:AISettings $AISettings_80241C90
{
    2.2 % move speed
    0` % move time
    0` % wait time
   70.0 % alert radius
    0.0
    1`
    3.2 % chase speed
    15`
    1`
   90.0 % chase radius
    0.0
    1`
}

#new:Script $Script_NpcAI_80241CC0
{
    0:  Call  DoBasicAI ( $AISettings_80241C90 )
   10:  Return
   18:  End
}

#new:NpcSettings $NpcSettings_80241CE0
{
	00000000 00170017 00000000 00000000 $Script_NpcAI_80241CC0 80077F70 00000000 8007809C
	00000000 00000000 000C0000
}

% Panners

#new:Script $Script_SetupTexturePan
{
	Call     802C9000 	( ~Model:nagare 1 )
	Thread
		Set  *Var[0] 1
		Set  *Var[1] 32
		Set  *Var[2] C8
		Set  *Var[3] 6E
		Set  *Var[4] 1F4
		Set  *Var[5] 1
		Set  *Var[6] 1
		Set  *Var[7] 1
		Set  *Var[8] 1
		Set  *Var[9] 0
		Set  *Var[A] 0
		Set  *Var[B] 0
		Set  *Var[C] 0
		Exec $Script_TexturePan_Update
	EndThread
	Return
	End
}

#new:Script $Script_TexturePan_Update
{
    SetGroup  00000000
    If  *Var[5]  ==  00000001
        If  *Var[6]  ==  00000001
            If  *Var[7]  ==  00000001
                If  *Var[8]  ==  00000001
                    Call  $Function_TexturePan_Simple ( )
                    Return
                EndIf
            EndIf
        EndIf
    EndIf
    Call  $Function_TexturePan_Complex ( )
    Return
    End
}

#new:Function $Function_TexturePan_Simple
{
    ADDIU       SP, SP, FFE8
    SW          S0, 10 (SP)
    COPY        S0, A0
    SW          RA, 14 (SP)
    LW          V1, A8 (S0)
    LW          V0, 88 (S0)
    ADDU        V1, V1, V0
    BGEZ        V1, .o30
    SW          V1, A8 (S0)
    LUI         V0, 2
    BEQ         R0, R0, .o40
    ADDU        V0, V1, V0
    .o30
    LUI         A0, 2
    SLT         V0, A0, V1
    BEQ         V0, R0, .o44
    SUBU        V0, V1, A0
    .o40
    SW          V0, A8 (S0)
    .o44
    LW          V1, AC (S0)
    LW          V0, 8C (S0)
    ADDU        V1, V1, V0
    BGEZ        V1, .o64
    SW          V1, AC (S0)
    LUI         V0, 2
    BEQ         R0, R0, .o74
    ADDU        V0, V1, V0
    .o64
    LUI         A0, 2
    SLT         V0, A0, V1
    BEQ         V0, R0, .o78
    SUBU        V0, V1, A0
    .o74
    SW          V0, AC (S0)
    .o78
    LW          V1, B0 (S0)
    LW          V0, 90 (S0)
    ADDU        V1, V1, V0
    BGEZ        V1, .o98
    SW          V1, B0 (S0)
    LUI         V0, 2
    BEQ         R0, R0, .oA8
    ADDU        V0, V1, V0
    .o98
    LUI         A0, 2
    SLT         V0, A0, V1
    BEQ         V0, R0, .oAC
    SUBU        V0, V1, A0
    .oA8
    SW          V0, B0 (S0)
    .oAC
    LW          V1, B4 (S0)
    LW          V0, 94 (S0)
    ADDU        V1, V1, V0
    BGEZ        V1, .oCC
    SW          V1, B4 (S0)
    LUI         V0, 2
    BEQ         R0, R0, .oDC
    ADDU        V0, V1, V0
    .oCC
    LUI         A0, 2
    SLT         V0, A0, V1
    BEQ         V0, R0, .oE0
    SUBU        V0, V1, A0
    .oDC
    SW          V0, B4 (S0)
    .oE0
    LW          A0, 84 (S0)
    JAL         8011BC1C
    LW          A1, A8 (S0)
    LW          A0, 84 (S0)
    JAL         8011BC34
    LW          A1, AC (S0)
    LW          A0, 84 (S0)
    JAL         8011BC4C
    LW          A1, B0 (S0)
    LW          A0, 84 (S0)
    JAL         8011BC64
    LW          A1, B4 (S0)
    LW          RA, 14 (SP)
    LW          S0, 10 (SP)
    CLEAR       V0
    JR          RA
    ADDIU       SP, SP, 18
}

#new:Function $Function_TexturePan_Complex
{
    ADDIU       SP, SP, FFE8
    SW          S0, 10 (SP)
    COPY        S0, A0
    BEQ         A1, R0, .o24
    SW          RA, 14 (SP)
    SW          R0, 70 (S0)
    SW          R0, 74 (S0)
    SW          R0, 78 (S0)
    SW          R0, 7C (S0)
    .o24
    LW          V0, 70 (S0)
    BNE         V0, R0, .o64
    NOP
    LW          V1, A8 (S0)
    LW          V0, 88 (S0)
    ADDU        V1, V1, V0
    BGEZ        V1, .o50
    SW          V1, A8 (S0)
    LUI         V0, 2
    BEQ         R0, R0, .o60
    ADDU        V0, V1, V0
    .o50
    LUI         A0, 2
    SLT         V0, A0, V1
    BEQ         V0, R0, .o64
    SUBU        V0, V1, A0
    .o60
    SW          V0, A8 (S0)
    .o64
    LW          V0, 74 (S0)
    BNE         V0, R0, .oA4
    NOP
    LW          V1, AC (S0)
    LW          V0, 8C (S0)
    ADDU        V1, V1, V0
    BGEZ        V1, .o90
    SW          V1, AC (S0)
    LUI         V0, 2
    BEQ         R0, R0, .oA0
    ADDU        V0, V1, V0
    .o90
    LUI         A0, 2
    SLT         V0, A0, V1
    BEQ         V0, R0, .oA4
    SUBU        V0, V1, A0
    .oA0
    SW          V0, AC (S0)
    .oA4
    LW          V0, 78 (S0)
    BNE         V0, R0, .oE4
    NOP
    LW          V1, B0 (S0)
    LW          V0, 90 (S0)
    ADDU        V1, V1, V0
    BGEZ        V1, .oD0
    SW          V1, B0 (S0)
    LUI         V0, 2
    BEQ         R0, R0, .oE0
    ADDU        V0, V1, V0
    .oD0
    LUI         A0, 2
    SLT         V0, A0, V1
    BEQ         V0, R0, .oE4
    SUBU        V0, V1, A0
    .oE0
    SW          V0, B0 (S0)
    .oE4
    LW          V0, 7C (S0)
    BNE         V0, R0, .o124
    NOP
    LW          V1, B4 (S0)
    LW          V0, 94 (S0)
    ADDU        V1, V1, V0
    BGEZ        V1, .o110
    SW          V1, B4 (S0)
    LUI         V0, 2
    BEQ         R0, R0, .o120
    ADDU        V0, V1, V0
    .o110
    LUI         A0, 2
    SLT         V0, A0, V1
    BEQ         V0, R0, .o124
    SUBU        V0, V1, A0
    .o120
    SW          V0, B4 (S0)
    .o124
    LW          A0, 84 (S0)
    JAL         8011BC1C
    LW          A1, A8 (S0)
    LW          A0, 84 (S0)
    JAL         8011BC34
    LW          A1, AC (S0)
    LW          A0, 84 (S0)
    JAL         8011BC4C
    LW          A1, B0 (S0)
    LW          A0, 84 (S0)
    JAL         8011BC64
    LW          A1, B4 (S0)
    LW          V0, 70 (S0)
    LW          V1, 78 (S0)
    LW          A0, 98 (S0)
    ADDIU       V0, V0, 1
    SW          V0, 70 (S0)
    LW          V0, 74 (S0)
    ADDIU       V1, V1, 1
    SW          V1, 78 (S0)
    LW          V1, 70 (S0)
    ADDIU       V0, V0, 1
    SW          V0, 74 (S0)
    LW          V0, 7C (S0)
    SLT         V1, V1, A0
    ADDIU       V0, V0, 1
    BNE         V1, R0, .o198
    SW          V0, 7C (S0)
    SW          R0, 70 (S0)
    .o198
    LW          V0, 74 (S0)
    LW          V1, 9C (S0)
    SLT         V0, V0, V1
    BEQL        V0, R0, .o1AC
    SW          R0, 74 (S0)
    .o1AC
    LW          V0, 78 (S0)
    LW          V1, A0 (S0)
    SLT         V0, V0, V1
    BEQL        V0, R0, .o1C0
    SW          R0, 78 (S0)
    .o1C0
    LW          V0, 7C (S0)
    LW          V1, A4 (S0)
    SLT         V0, V0, V1
    BEQL        V0, R0, .o1D4
    SW          R0, 7C (S0)
    .o1D4
    LW          RA, 14 (SP)
    LW          S0, 10 (SP)
    CLEAR       V0
    JR          RA
    ADDIU       SP, SP, 18
}

%

#new:Function $Function_GetTattle
{
LIO       V0, $String_Tattle
JR        RA
RESERVED
}

#string $String_Tattle {
[Style Tattle][EnableCDownNext]
It seems that we have to press[BR]
those buttons in a specific order,[BR]
I wonder which one is the correct[BR]
one?
[Wait][End]      
}